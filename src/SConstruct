import sys
Import('env')

# Omits the 'test' target 
all = env.Alias("all", ["wesnoth", "wesnoth_editor", "wesnothd", "campaignd",
                        "cutter", "exploder"])
env.Default("all")

#
# Implement configuration switches
#
extralibs=[]

if env["debug"]:
    env["CXXFLAGS"] = Split("-O0 -DDEBUG -ggdb3 -W -Wall -ansi")
else:
    env["CXXFLAGS"] = Split("-O2 -ansi")

if env['static']:
    env["LDFLAGS"].append("-all-static")

if env['profile']:
    env["CXXFLAGS"].append("-pg")

if env['strict']:
    env["CXXFLAGS"].append("-Werror -Wno-unused -Wno-sign-compare")

if env['tinygui']:
    env["CXXFLAGS"].append("-DUSE_TINY_GUI")

if env['smallgui']:
    env["CXXFLAGS"].append("-DUSE_SMALL_GUI")

if env['lowmem']:
    env["CXXFLAGS"].append("-DLOW_MEM")

if env['fribidi']:
        env["CXXFLAGS"].append("-DHAVE_FRIBIDI")
        extralibs.append("fribidi")

if env['raw_sockets']:
    env["CXXFLAGS"].append("-DNETWORK_USE_RAW_SOCKETS")

cc_version = env["CCVERSION"]
if env["CC"] == "gcc":
    (major, minor, rev) = map(int, cc_version.split("."))
    if major*10+minor < 33:
        print "Your compiler version is too old"
        Exit(1)

#
# Libraries and source groups
#
boost_libs = Split("boost_iostreams-mt boost_regex")
SDL_libs = Split("SDL_net SDL_ttf SDL_mixer SDL_image SDL")
commonlibs = SDL_libs + boost_libs + ["pthread", "-lpython"+sys.version[:3]]

#color_range.cpp should be removed, but game_config depends on it.
#game_config has very few things that are needed elsewhere, it should be
#removed.  Requires moving path and version at least to other files.

libwesnoth_core_sources = [
    "color_range.cpp",
    "config.cpp",
    "filesystem.cpp",
    "game_config.cpp",
    "gettext.cpp",
    "log.cpp",
    "map.cpp",
    "network.cpp",
    "network_worker.cpp",
    "thread.cpp",
    "tstring.cpp",
    "util.cpp",
    "serialization/binary_or_text.cpp",
    "serialization/binary_wml.cpp",
    "serialization/parser.cpp",
    "serialization/preprocessor.cpp",
    "serialization/string_utils.cpp",
    "serialization/tokenizer.cpp",
    ]
env.Library("wesnoth_core", libwesnoth_core_sources, 
            CPPPATH = ['.', 'src/serialization', "/usr/include/SDL"])

libwesnoth_sources = [
    "astarnode.cpp",
    "astarsearch.cpp",
    "builder.cpp",
    "cavegen.cpp",
    "clipboard.cpp",
    "construct_dialog.cpp",
    "cursor.cpp",
    "display.cpp",
    "events.cpp",
    "filechooser.cpp",
    "font.cpp",
    "generic_event.cpp",
    "hotkeys.cpp",
    "image.cpp",
    "key.cpp",
    "language.cpp",
    "loadscreen.cpp",
    "map_create.cpp",
    "map_label.cpp",
    "mapgen.cpp",
    "mapgen_dialog.cpp",
    "marked-up_text.cpp",
    "minimap.cpp",
    "pathutils.cpp",
    "preferences.cpp",
    "preferences_display.cpp",
    "race.cpp",
    "random.cpp",
    "reports.cpp",
    "show_dialog.cpp",
    "sound.cpp",
    "soundsource.cpp",
    "terrain.cpp",
    "terrain_translation.cpp",
    "tooltips.cpp",
    "video.cpp",
    "theme.cpp",
    "widgets/button.cpp",
    "widgets/file_menu.cpp",
    "widgets/label.cpp",
    "widgets/menu.cpp",
    "widgets/menu_style.cpp",
    "widgets/progressbar.cpp",
    "widgets/scrollarea.cpp",
    "widgets/scrollbar.cpp",
    "widgets/slider.cpp",
    "widgets/textbox.cpp",
    "widgets/widget.cpp",
    "wml_exception.cpp",
    ]
env.Library("wesnoth", libwesnoth_sources, 
            CPPPATH = ['.', 'src/serialization', "/usr/include/SDL"])

libwesnothd_sources = [
    "loadscreen_empty.cpp",
    "tools/dummy_video.cpp",
    ]
env.Library("wesnothd", libwesnothd_sources, 
            CPPPATH = ['.', "/usr/include/SDL"])

libcampaignd_sources = [
    "publish_campaign.cpp",
    ]
env.Library("campaignd", libcampaignd_sources, 
            CPPPATH = ['.', "/usr/include/SDL"])

libwesnoth_sdl_sources = [
    "sdl_utils.cpp",
    ]
env.Library("wesnoth_sdl", libwesnoth_sdl_sources, 
            CPPPATH = ['.', "/usr/include/SDL"])

libcutter_sources = [
    "tools/exploder_utils.cpp",
    "tools/exploder_cutter.cpp",
    ]
env.Library("cutter", libcutter_sources, 
            CPPPATH = ['.', "/usr/include/SDL"])

# Used by both 'wesnoth' and 'test' targets
wesnoth_sources = [
    "about.cpp",
    "actions.cpp",
    "ai.cpp",
    "ai_dfool.cpp",
    "ai_attack.cpp",
    "ai_move.cpp",
    "ai_python.cpp",
    "ai_village.cpp",
    "animated_game.cpp",
    "attack_prediction.cpp",
    "callable_objects.cpp",
    "config_adapter.cpp",
    "dialogs.cpp",
    "floating_textbox.cpp",
    "formula.cpp",
    "formula_ai.cpp",
    "formula_function.cpp",
    "formula_tokenizer.cpp",
    "game_display.cpp",
    "game_events.cpp",
    "game_preferences.cpp",
    "game_preferences_display.cpp",
    "gamestatus.cpp",
    "generate_report.cpp",
    "halo.cpp",
    "help.cpp",
    "intro.cpp",
    "leader_list.cpp",
    "menu_events.cpp",
    "mouse_events.cpp",
    "multiplayer.cpp",
    "multiplayer_ui.cpp",
    "multiplayer_wait.cpp",
    "multiplayer_connect.cpp",
    "multiplayer_create.cpp",
    "multiplayer_lobby.cpp",
    "pathfind.cpp",
    "playcampaign.cpp",
    "play_controller.cpp",
    "playmp_controller.cpp",
    "playsingle_controller.cpp",
    "playturn.cpp",
    "replay.cpp",
    "replay_controller.cpp",
    "sha1.cpp",
    "settings.cpp",
    "statistics.cpp",
    "team.cpp",
    "terrain_filter.cpp",
    "titlescreen.cpp",
    "unit.cpp",
    "unit_abilities.cpp",
    "unit_animation.cpp",
    "unit_display.cpp",
    "unit_frame.cpp",
    "unit_map.cpp",
    "unit_types.cpp",
    "upload_log.cpp",
    "variable.cpp",
    "variant.cpp",
    "widgets/combo.cpp",
    "widgets/scrollpane.cpp",
]

#
# Target declarations
#

env.Program("wesnoth", ["game.cpp"] + wesnoth_sources,
            CPPPATH = ['.', 'server', "/usr/include/SDL"],
            LIBS = ['wesnoth_core', 'wesnoth_sdl', 'wesnoth', 'campaignd'] + commonlibs + extralibs,
            LIBPATH = [".", "/lib", "/usr/lib"])

wesnoth_editor_sources = [
    "editor/editor.cpp",
    "editor/editor_layout.cpp",
    "editor/map_manip.cpp",
    "editor/editor_display.cpp",
    "editor/editor_palettes.cpp",
    "editor/editor_main.cpp",
    "editor/editor_dialogs.cpp",
    "editor/editor_undo.cpp",
    "animated_editor.cpp",
    "gamestatus_editor.cpp",
    ]
env.Program("wesnoth_editor", wesnoth_editor_sources,
            CPPPATH = ['.', "/usr/include/SDL"],
            LIBS = ['wesnoth_core', 'wesnoth_sdl', 'wesnoth'] + commonlibs + extralibs,
            LIBPATH = [".", "/lib", "/usr/lib"])

campaignd_sources = [
    "campaign_server/campaign_server.cpp",
    ]
env.Program("campaignd", campaignd_sources,
            CPPPATH = ['.', 'server', "/usr/include/SDL"],
            LIBS = ['wesnoth_core', 'wesnothd', 'campaignd', 'wesnoth'] + commonlibs,
            LIBPATH = [".", "/lib", "/usr/lib"])

wesnothd_sources = [
    "server/game.cpp",
    "server/input_stream.cpp",
    "server/metrics.cpp",
    "server/player.cpp",
    "server/proxy.cpp",
    "server/server.cpp",
    "server/simple_wml.cpp",
    ]
env.Program("wesnothd", wesnothd_sources,
            CPPPATH = ['.', 'server', "/usr/include/SDL"],
            LIBS =  ['wesnoth_core', 'wesnothd'] + commonlibs,
            LIBPATH = [".", "/lib", "/usr/lib"])

cutter_sources = [
    "tools/cutter.cpp",
    ]
env.Program("cutter", cutter_sources,
            CPPPATH = ['.', "/usr/include/SDL"],
            LIBS =  ['cutter', 'wesnoth_core', 'wesnoth_sdl', 'wesnothd', 'wesnoth'] + commonlibs,
            LIBPATH = [".", "/lib", "/usr/lib"])

exploder_sources = [
	"tools/exploder.cpp",
	"tools/exploder_composer.cpp",
        ]
env.Program("exploder", exploder_sources,
            CPPPATH = ['.', "/usr/include/SDL"],
            LIBS =  ['cutter', 'wesnoth_core', 'wesnoth_sdl', 'wesnothd', 'wesnoth'] + commonlibs,
            LIBPATH = [".", "/lib", "/usr/lib"])

test_sources = [
	"tests/main.cpp",
	"tests/test_util.cpp",
        ]
env.Program("test", test_sources,
            CPPPATH = ['.', "/usr/include/SDL"],
            LIBS =  ['wesnoth_core', 'wesnoth_sdl', 'wesnothd'] + commonlibs,
            LIBPATH = [".", "/lib", "/usr/lib"])

# FIXME: Include this in gameconfig.cpp when we switch over to scons.
# Because of the content check, scons will do the right thing.
# At that point -DSVNREV can be removed from CXXFLAGS.
#r = env.Command("revision_stamp.h", [],
#            'echo "#define REVISION \"%s\"" >revision_stamp.h' % svnrev)
#env.AlwaysBuild(r)

#
# File inventory, for archive makes abd analysis tools
#
headers = [
    "tools/exploder_composer.hpp",
    "tools/exploder_utils.hpp",
    "tools/exploder_cutter.hpp",
    "serialization/tokenizer.hpp",
    "serialization/parser.hpp",
    "serialization/binary_or_text.hpp",
    "serialization/binary_wml.hpp",
    "serialization/preprocessor.hpp",
    "serialization/string_utils.hpp",
    "widgets/progressbar.hpp",
    "widgets/textbox.hpp",
    "widgets/combo.hpp",
    "widgets/file_menu.hpp",
    "widgets/scrollpane.hpp",
    "widgets/menu.hpp",
    "widgets/button.hpp",
    "widgets/label.hpp",
    "widgets/slider.hpp",
    "widgets/scrollbar.hpp",
    "widgets/widget.hpp",
    "widgets/scrollarea.hpp",
    "server/player.hpp",
    "server/game.hpp",
    "server/input_stream.hpp",
    "server/proxy.hpp",
    "server/metrics.hpp",
    "editor/editor_undo.hpp",
    "editor/map_manip.hpp",
    "editor/editor_layout.hpp",
    "editor/editor.hpp",
    "editor/editor_palettes.hpp",
    "editor/editor_dialogs.hpp",
    "about.hpp",
    "actions.hpp",
    "ai.hpp",
    "ai2.hpp",
    "ai_dfool.hpp",
    "ai_interface.hpp",
    "ai_python.hpp",
    "animated.hpp",
    "animated.i",
    "array.hpp",
    "astarnode.hpp",
    "attack_prediction.hpp",
    "builder.hpp",
    "cavegen.hpp",
    "clipboard.hpp",
    "color_range.hpp",
    "config.hpp",
    "config_adapter.hpp",
    "construct_dialog.hpp",
    "cursor.hpp",
    "dialogs.hpp",
    "display.hpp",
    "events.hpp",
    "file_chooser.hpp",
    "filesystem.hpp",
    "floating_textbox.hpp",
    "font.hpp",
    "game_config.hpp",
    "game_display.hpp",
    "game_errors.hpp",
    "game_events.hpp",
    "game_preferences.hpp",
    "gamestatus.hpp",
    "generic_event.hpp",
    "gettext.hpp",
    "global.hpp",
    "halo.hpp",
    "help.hpp",
    "hotkeys.hpp",
    "image.hpp",
    "intro.hpp",
    "key.hpp",
    "language.hpp",
    "leader_list.hpp",
    "loadscreen.hpp",
    "log.hpp",
    "map.hpp",
    "map_create.hpp",
    "map_label.hpp",
    "mapgen.hpp",
    "mapgen_dialog.hpp",
    "marked-up_text.hpp",
    "menu_events.hpp",
    "minimap.hpp",
    "mouse_events.hpp",
    "multiplayer.hpp",
    "multiplayer_connect.hpp",
    "multiplayer_create.hpp",
    "multiplayer_lobby.hpp",
    "multiplayer_ui.hpp",
    "multiplayer_wait.hpp",
    "network.hpp",
    "network_worker.hpp",
    "pathfind.hpp",
    "pathutils.hpp",
    "play_controller.hpp",
    "playcampaign.hpp",
    "playmp_controller.hpp",
    "playsingle_controller.hpp",
    "playturn.hpp",
    "preferences.hpp",
    "preferences_display.hpp",
    "publish_campaign.hpp",
    "race.hpp",
    "random.hpp",
    "replay.hpp",
    "replay_controller.hpp",
    "reports.hpp",
    "scoped_resource.hpp",
    "sha1.hpp",
    "settings.hpp",
    "sdl_utils.hpp",
    "show_dialog.hpp",
    "sound.hpp",
    "soundsource.hpp",
    "statistics.hpp",
    "team.hpp",
    "terrain.hpp",
    "terrain_filter.hpp",
    "terrain_translation.hpp",
    "theme.hpp",
    "thread.hpp",
    "titlescreen.hpp",
    "tooltips.hpp",
    "tstring.hpp",
    "unit.hpp",
    "unit_abilities.hpp",
    "unit_animation.hpp",
    "unit_display.hpp",
    "unit_frame.hpp",
    "unit_map.hpp",
    "unit_types.hpp",
    "upload_log.hpp",
    "util.hpp",
    "variable.hpp",
    "video.hpp",
    "wml_separators.hpp",
    "wesconfig.h",
    "wml_exception.hpp",
]

sources =   libwesnoth_sources + libwesnoth_core_sources + \
            libwesnothd_sources + libcampaignd_sources + \
            libwesnoth_sdl_sources + libcutter_sources + \
            wesnoth_editor_sources + campaignd_sources + wesnothd_sources + \
            cutter_sources + exploder_sources + test_sources

#
# Utility productions
#

env.Command("TAGS", sources, 'etags -l c++ $SOURCES')
env.Clean(all, 'TAGS')

# Local variables:
# mode: python
# end:
