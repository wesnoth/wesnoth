/**
 * @author Timotei Dolean
 */
 //grammar org.wesnoth.WML with org.eclipse.xtext.common.Terminals
grammar org.wesnoth.WML hidden(EOL, WS, SL_COMMENT)
generate wML "http://www.wesnoth.org/WML"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

//TODO: add cross-reference for variables
//handles TODO:
// - preprocessor commands: #define, #enddef, etc
// - fix: { { } }

WMLRoot:
	( Tags += WMLTag
	| MacroCalls += WMLMacroCall
	| MacroDefines += WMLMacroDefine
	| Textdomains += WMLTextdomain
	)*
	;

WMLTag:
	'[' (plus?='+')? name=ID ']'
		( Tags += WMLTag
		| Keys += WMLKey
		| MacroCalls += WMLMacroCall // this generates multiple alternatives
		| MacroDefines += WMLMacroDefine
		| Textdomains += WMLTextdomain
		)*
	'[/' endName = ID ']'
	;

WMLKey hidden(WS):
	(name = ID) '=' value += WMLKeyValue+ eol=(EOL|SL_COMMENT);

WMLKeyValue:
	WMLValue | WMLMacroCall | WMLLuaCode | WMLArrayCall;
	
WMLMacroCall:
	'{' (relative?='~')? name=ID
		( params += (WMLValue | WMLMacroParameter)
		| extraMacros += WMLMacroCall
		)*
	'}';
	
WMLMacroParameter: {WMLMacroParameter}
	'(' param += (WMLValue | WMLTag | WMLMacroCall | WMLKey)* ')';
WMLLuaCode:
	value = LUA_CODE;
WMLArrayCall:
	'[' value += WMLValue+ ']' ;
WMLMacroDefine:
	name = DEFINE;
//		( Tags += WMLTag
//		| Keys += WMLKey
//		| MacroCalls += WMLMacroCall
//		| MacroDefines += WMLMacroDefine
//		| Textdomains += WMLTextdomain
//		)*;
//		ENDDEF;
	
WMLTextdomain:
	name = TEXTDOMAIN;

WMLValue:
	value = (ID | STRING |'+'|'~'| ANY_OTHER	);

terminal LUA_CODE   : '<<' -> '>>';
terminal DEFINE     : '#define' -> '#enddef';//!('\n'|'\r')* ('\r'? '\n')?;
//terminal ENDDEF     : '#enddef' !('\n'|'\r')* ('\r'? '\n')?;
terminal TEXTDOMAIN : '#textdomain' !('\n'|'\r')* ('\r'? '\n')?;
terminal STRING     : '"' ( '\\' ('b'|'t'|'n'|'f'|'r'|'"'|"'"|'\\') | !('\\'|'"') )* '"';
terminal ID         : ('a'..'z'|'A'..'Z'|'0'..'9'|'_'|'-'|',')+;
terminal EOL		 : ('\r'|'\n');
terminal WS         : (' '|'\t')+;
terminal ANY_OTHER  : .;
terminal SL_COMMENT : '#' !('\n'|'\r')* ('\r'? '\n')?;
