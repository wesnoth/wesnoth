#!/bin/sed -Ef
# 20221025 05:35:29 info server: 2601:243:4101:5740:4c58:4587:3b8e:d151   <Tourgus> can you believe this shit
s/	/ /g
# skip over irregular lines
/^[0-9]+ [0-2][0-9]:[0-5][0-9]:[0-5][0-9] [[:alpha:]]+ [[:alpha:]]+:/!d
# remove the timestamp and the IP
#s/^\(PREV: \|OLD: \|\)[0-9]\+ [0-2][0-9]:[0-5][0-9]:[0-5][0-9] [a-zA-Z]\+ [a-zA-Z]\+: [0-9]\+\.[0-9]\+\.[0-9]\+\.[0-9]\+ /\1/
s/^(PREV: |OLD: |)[0-9]+ [0-2][0-9]:[0-5][0-9]:[0-5][0-9] [[:alpha:]]+ [[:alpha:]]+: [[:xdigit:].:]+ +/\1/
s/ info server://
s/""/"/g
/^<[[:alnum:]_-]+ .*>$|^<[[:alnum:]_-]+>|^[0-9]+ [0-2][0-9]:[0-5][0-9]:[0-5][0-9] (<server>|Statistics:) |^[[:alnum:]_-]+ ((started( reloaded|)|advanced|ended) game: |.* and disconnected\.|has logged o)/!d
s/^([0-9]+ [0-2][0-9]:[0-5][0-9]:[0-5][0-9]) <server>/14\1 <05server14>05/
s/^<([[:alnum:]_-]+) (.*)>$/14<12\1 \214>/
s/^<([[:alnum:]_-]+)>/                     14<12\114>6/;s/ *(.{27}14>)/\1/
s/^[[:alnum:]_-]+ started game: .*$/03&/
s/^[[:alnum:]_-]+ started reloaded game: .*$/03&/
s/^[[:alnum:]_-]+ advanced game: .*$/03&/
s/^[[:alnum:]_-]+ ended game: .*$/05&/
s/^[[:alnum:]_-]+ (has logged o|.* and disconnected.).*$/14&/
s/^[0-9]+ [0-2][0-9]:[0-5][0-9]:[0-5][0-9] Statistics:.*$/14&/
